C      _______              __
C     / ___/ /  ___  __ _  / /  ___
C    / /__/ _ \/ _ \/  V \/ _ \/ _ \
C    \___/_//_/\___/_/_/_/_.__/\___/
C    Please refer to Copyright.txt, in Chombo's root directory.

#include "CONSTANTS.H"
      subroutine extraptoghostvel(
     $     chf_fra[vel],
     $     chf_box[lobox],
     $     chf_const_int[haslo],
     $     chf_box[hibox],
     $     chf_const_int[hashi],
     $     chf_const_int[facedir],
     $     chf_const_int[ncomp]
     $     )

      integer chf_ddecl[ii,i;jj,j;kk,k], icomp

      chf_dterm[
      ii = chf_id(facedir, 0);
      jj = chf_id(facedir, 1);
      kk = chf_id(facedir, 2)]
      
      if(hashi.eq. 1) then

         do icomp = 0, ncomp-1
            chf_multido[hibox;i;j;k]

            vel(chf_ix[i;j;k],icomp) =
     $           2*vel(chf_ix[i-  ii;j-  jj;k-  kk], icomp)
     $           - vel(chf_ix[i-2*ii;j-2*jj;k-2*kk], icomp) 

            chf_enddo
         enddo
      endif

      if(haslo .eq. 1) then

         do icomp = 0, ncomp-1
            chf_multido[lobox;i;j;k]

            vel(chf_ix[i;j;k],icomp) =
     $           2*vel(chf_ix[i+  ii;j+  jj;k+  kk], icomp)
     $           - vel(chf_ix[i+2*ii;j+2*jj;k+2*kk], icomp) 

            chf_enddo
         enddo
      endif

      return 
      end
cccccccccccccccccccccccccccccccccc
c warning: this routine may be triggering for those
c     with sensitivities about code aesthetics
ccccccccccccccccccccccccccccccccc
      subroutine applyopvtopnobcs(
     &     chf_fra[lphfab],
     &     chf_const_fra[phifab],
     &     chf_const_fra1[acofab],
     $     chf_const_fra1[eta0fab],
     $     chf_const_fra1[eta1fab],
     $     chf_const_fra1[eta2fab],
     $     chf_const_fra1[lam0fab],
     $     chf_const_fra1[lam1fab],
     $     chf_const_fra1[lam2fab],
     $     chf_const_real[dx],
     $     chf_const_real[alpha],
     $     chf_const_real[beta],
     $     chf_box[region])


c     gee, that is a lot of temporary variables.
c     I wonder what fiendish hell awaits me?
c     
c     phi, divf---0 1 2 are variables
      real_t divf(0:CH_SPACEDIM-1)
      real_t aphi(0:CH_SPACEDIM-1)
c     here 0, 1 2 refers to the face directions.  charming, no?
      real_t  etaL(0:CH_SPACEDIM-1)
      real_t  etaH(0:CH_SPACEDIM-1)
      real_t  lamL(0:CH_SPACEDIM-1)
      real_t  lamH(0:CH_SPACEDIM-1)

c     here the first index is the variable, the second is the face direction
      real_t  fluxL(0:CH_SPACEDIM-1,0:CH_SPACEDIM-1)
      real_t  fluxH(0:CH_SPACEDIM-1,0:CH_SPACEDIM-1)

c     here the first index is the variable, the second is the derivative direction, third is the face direction
      real_t  gphiL(0:CH_SPACEDIM-1,0:CH_SPACEDIM-1,0:CH_SPACEDIM-1)
      real_t  gphiH(0:CH_SPACEDIM-1,0:CH_SPACEDIM-1,0:CH_SPACEDIM-1)

c     index refers face direction
      real_t  divuL(0:CH_SPACEDIM-1)
      real_t  divuH(0:CH_SPACEDIM-1)

      integer chf_ddecl[i;j;k], facedir, derivdir, veldir
      integer chf_ddecl[iif;jjf;kkf]
      integer chf_ddecl[iid;jjd;kkd]

c     brace yourself, this is a long loop
      chf_multido[region; i; j; k]


      
      chf_dterm[
      etaL(0) =eta0fab(CHF_IX[i   ;j   ;k   ]);
      etaL(1) =eta1fab(CHF_IX[i   ;j   ;k   ]);
      etaL(2) =eta2fab(CHF_IX[i   ;j   ;k   ])]
      chf_dterm[                            
      etaH(0) =eta0fab(CHF_IX[i+1 ;j   ;k   ]);
      etaH(1) =eta1fab(CHF_IX[i   ;j+1 ;k   ]);
      etaH(2) =eta2fab(CHF_IX[i   ;j   ;k+1 ])]
      chf_dterm[
      lamL(0) =lam0fab(CHF_IX[i   ;j   ;k   ]);
      lamL(1) =lam1fab(CHF_IX[i   ;j   ;k   ]);
      lamL(2) =lam2fab(CHF_IX[i   ;j   ;k   ])]
      chf_dterm[                            
      lamH(0) =lam0fab(CHF_IX[i+1 ;j   ;k   ]);
      lamH(1) =lam1fab(CHF_IX[i   ;j+1 ;k   ]);
      lamH(2) =lam2fab(CHF_IX[i   ;j   ;k+1 ])]
      

c     here the first index is the variable, the second is the derivative direction, third is the face direction
      do facedir = 0, CH_SPACEDIM-1
         chf_dterm[
         iif = chf_id(facedir, 0);
         jjf = chf_id(facedir, 1);
         kkf = chf_id(facedir, 2)]

         do derivdir = 0, CH_SPACEDIM-1
            chf_dterm[
            iid = chf_id(derivdir, 0);
            jjd = chf_id(derivdir, 1);
            kkd = chf_id(derivdir, 2)]

            do veldir = 0, CH_SPACEDIM-1

               if(facedir .eq. derivdir) then

                  gphiH(veldir, derivdir ,facedir) = (phifab(chf_ix[i+iid;j+jjd;k+kkd],veldir) - phifab(chf_ix[i    ;j    ;k    ],veldir))/dx
                  gphiL(veldir, derivdir ,facedir) = (phifab(chf_ix[i    ;j    ;k    ],veldir) - phifab(chf_ix[i-iid;j-jjd;k-kkd],veldir))/dx

               else

                  gphiH(veldir, derivdir ,facedir) = (one/(four*dx))*(
     $                 phifab(chf_ix[i+iid+iif;j+jjd+jjf;k+kkd+kkf],veldir) - phifab(chf_ix[i-iid+iif;j-jjd+jjf;k-kkd+kkf],veldir) + 
     $                 phifab(chf_ix[i+iid    ;j+jjd    ;k+kkd    ],veldir) - phifab(chf_ix[i-iid    ;j-jjd    ;k-kkd    ],veldir)  )

                  gphiL(veldir, derivdir ,facedir) = (one/(four*dx))*(
     $                 phifab(chf_ix[i+iid-iif;j+jjd-jjf;k+kkd-kkf],veldir) - phifab(chf_ix[i-iid-iif;j-jjd-jjf;k-kkd-kkf],veldir) + 
     $                 phifab(chf_ix[i+iid    ;j+jjd    ;k+kkd    ],veldir) - phifab(chf_ix[i-iid    ;j-jjd    ;k-kkd    ],veldir)  )

               endif
            enddo
         enddo
      enddo

      do facedir = 0, CH_SPACEDIM-1
         divuL(facedir) = zero
         divuH(facedir) = zero
         do veldir = 0, CH_SPACEDIM-1
            divuL(facedir)= divuL(facedir) + gphiL(veldir, veldir, facedir)
            divuH(facedir)= divuH(facedir) + gphiH(veldir, veldir, facedir)
         enddo
      enddo

c     here the first index is the variable, the second is the face direction
      do facedir = 0, CH_SPACEDIM-1
         do veldir = 0, CH_SPACEDIM-1
            fluxL(veldir, facedir) = etaL(facedir)*(gphiL(facedir, veldir, facedir) + gphiL(veldir, facedir, facedir))
            fluxH(veldir, facedir) = etaH(facedir)*(gphiH(facedir, veldir, facedir) + gphiH(veldir, facedir, facedir))
            if(veldir .eq. facedir) then
               fluxL(veldir, facedir) = fluxL(veldir, facedir) + lamL(facedir)*divuL(facedir)
               fluxH(veldir, facedir) = fluxH(veldir, facedir) + lamH(facedir)*divuH(facedir)
            endif
         enddo
      enddo

      do veldir = 0, CH_SPACEDIM-1
         divf(veldir) = zero
         do facedir = 0, CH_SPACEDIM-1
            divf(veldir) = divf(veldir)
     $           + (fluxH(veldir, facedir)-fluxL(veldir, facedir))/dx
         enddo
      enddo

      chf_dterm[
      aphi(0) = acofab(CHF_IX[i;j;k])*phifab(CHF_IX[i;j;k], 0);
      aphi(1) = acofab(CHF_IX[i;j;k])*phifab(CHF_IX[i;j;k], 1);
      aphi(2) = acofab(CHF_IX[i;j;k])*phifab(CHF_IX[i;j;k], 2)]

      chf_dterm[
      lphfab(chf_ix[i;j;k],0) =  alpha*aphi(0) + beta*divf(0);
      lphfab(chf_ix[i;j;k],1) =  alpha*aphi(1) + beta*divf(1);
      lphfab(chf_ix[i;j;k],2) =  alpha*aphi(2) + beta*divf(2)]

c     told you it is  a long loop
      chf_enddo

      ch_flops=ch_flops+CHF_POINTS[region]*(4 + 6*CH_SPACEDIM + 12*(CH_SPACEDIM*CH_SPACEDIM))
      return

      end
cccccccccccccccccccccccccccccccccc
c warning: this routine may be triggering for those
c     with sensitivities about code aesthetics
ccccccccccccccccccccccccccccccccc
      subroutine getfluxvtopnobcs(
     &     chf_fra[flux],
     &     chf_const_fra[phifab],
     $     chf_const_fra1[etafab],
     $     chf_const_fra1[lamfab],
     $     chf_const_real[dx],
     $     chf_const_int[facedir],
     $     chf_const_real[beta],
     $     chf_box[facebox])

c     gee, that is a lot of temporary variables.
c     I wonder what fiendish hell awaits me?

c     here the first index is the variable, the second is the derivative direction, third is the face direction
      real_t  gphi(0:CH_SPACEDIM-1,0:CH_SPACEDIM-1)
      real_t  divu, eta, lam, fluxpt

      integer chf_ddecl[i;j;k] , derivdir, veldir
      integer chf_ddecl[iif;jjf;kkf]
      integer chf_ddecl[iid;jjd;kkd]


      chf_multido[facebox; i; j; k]

      eta = etafab(CHF_IX[i   ;j   ;k   ]);
      lam = lamfab(CHF_IX[i   ;j   ;k   ]);

      chf_dterm[
      iif = chf_id(facedir, 0);
      jjf = chf_id(facedir, 1);
      kkf = chf_id(facedir, 2)]

      do derivdir = 0, CH_SPACEDIM-1
         
         chf_dterm[
         iid = chf_id(derivdir, 0);
         jjd = chf_id(derivdir, 1);
         kkd = chf_id(derivdir, 2)]

         do veldir = 0, CH_SPACEDIM-1

            if(facedir .eq. derivdir) then

               gphi(veldir, derivdir) = (phifab(chf_ix[i;j;k],veldir) - phifab(chf_ix[i-iid;j-jjd;k-kkd],veldir))/dx

            else

               gphi(veldir, derivdir) = (one/(four*dx))*(
     $              phifab(chf_ix[i+iid-iif;j+jjd-jjf;k+kkd-kkf],veldir) - phifab(chf_ix[i-iid-iif;j-jjd-jjf;k-kkd-kkf],veldir) + 
     $              phifab(chf_ix[i+iid    ;j+jjd    ;k+kkd    ],veldir) - phifab(chf_ix[i-iid    ;j-jjd    ;k-kkd    ],veldir)  )

            endif

         enddo
      enddo

      divu = zero
      do veldir = 0, CH_SPACEDIM-1
         divu = divu + gphi(veldir, veldir)
      enddo

      do veldir = 0, CH_SPACEDIM-1
         fluxpt = eta*(gphi(facedir, veldir) + gphi(veldir, facedir))
         if(veldir .eq. facedir) then
            fluxpt = fluxpt  + lam*divu
         endif
c     for some reason, beta has to be multiplied in.
         flux(chf_ix[i;j;k], veldir) = beta*fluxpt
      enddo


      chf_enddo

      return

      end
ccccccccccccccccccccccccccccccccccc


      subroutine prolongvtop(
     &    chf_fra[phi],
     &    chf_const_fra[coarse],
     &    chf_box[region],
     &    chf_const_int[m])

      integer ncomp, n
      integer chf_ddecl[i;j;k]
      integer chf_ddecl[ii;jj;kk]

      ncomp = chf_ncomp[phi]
      do n = 0, ncomp-1
          chf_multido[region; i; j; k]
          chf_dterm[
          ii = (i-abs(mod(i,m)))/m;
          jj = (j-abs(mod(j,m)))/m;
          kk = (k-abs(mod(k,m)))/m]
          phi(chf_ix[i;j;k],n) =  phi(chf_ix[i;j;k],n) +
     &        coarse(chf_ix[ii;jj;kk],n)
         chf_enddo
      enddo

      return

      end
      subroutine restrictresvtop(
     &     chf_fra[res],
     &     chf_const_fra[resfine],
     &     chf_box[region],
     $     chf_const_int[ncomp])

      real_t denom
      integer n

      integer chf_ddecl[i;j;k]
      integer chf_ddecl[ii;jj;kk]

      denom = chf_dterm[two; *two; *two]

      do n = 0, ncomp-1
         chf_multido[region; i; j; k]
         chf_dterm[
         ii = (i-abs(mod(i,2)))/2;
         jj = (j-abs(mod(j,2)))/2;
         kk = (k-abs(mod(k,2)))/2]
         res(chf_ix[ii;jj;kk],n) = res(chf_ix[ii;jj;kk],n) + resfine(chf_ix[i;j;k],n)/denom

         chf_enddo
      enddo

      return
      end
      subroutine cellgradvtop(
     $     chf_fra1[grad],
     $     chf_const_fra1[vel],
     $     chf_box[grid],
     $     chf_const_real[dx],
     $     chf_const_int[divdir])

      integer chf_ddecl[ii,i;jj,j;kk,k]

      chf_dterm[
      ii = chf_id(divdir, 0);
      jj = chf_id(divdir, 1);
      kk = chf_id(divdir, 2)]

      chf_multido[grid;i;j;k]

      grad(chf_ix[i;j;k]) =
     $     (    vel(chf_ix[i+ii;j+jj;k+kk])
     $     -    vel(chf_ix[i-ii;j-jj;k-kk]) )/(two*dx)

      chf_enddo

      return
      end

      subroutine addgradtofluxvtop(
     $     chf_fra[flux],
     $     chf_const_fra1[eta],
     $     chf_const_int[fluxcomp],
     $     chf_const_fra[grad],
     $     chf_const_int[gradcomp],
     $     chf_const_int[gradtran],
     $     chf_box[regionface])

      integer chf_ddecl[i;j;k]
      real_t gradcontrib, trancontrib, etafac
      chf_multido[regionface;i;j;k]

      etafac      = eta(chf_ix[i;j;k])
      gradcontrib = grad(chf_ix[i;j;k], gradcomp)
      trancontrib = grad(chf_ix[i;j;k], gradtran)

      flux(chf_ix[i;j;k], fluxcomp) =  flux(chf_ix[i;j;k], fluxcomp) +
     $     eta(chf_ix[i;j;k])*
     $     ( grad(chf_ix[i;j;k], gradcomp)
     $     + grad(chf_ix[i;j;k], gradtran) )

      chf_enddo

      return
      end

      subroutine getfacegradvtop(
     $     chf_fra1[gradvelface],
     $     chf_const_fra1[gradvelcell],
     $     chf_const_fra1[velcomp],
     $     chf_box[region],
     $     chf_box[centerbox],
     $     chf_box[lobox],
     $     chf_const_int[haslo],
     $     chf_box[hibox],
     $     chf_const_int[hashi],
     $     chf_const_real[dx],
     $     chf_const_int[facedir],
     $     chf_const_int[divdir]
     $     )

      integer chf_ddecl[ii,i;jj,j;kk,k]

      chf_dterm[
      ii = chf_id(facedir, 0);
      jj = chf_id(facedir, 1);
      kk = chf_id(facedir, 2)]

      if (facedir .eq. divdir) then

         chf_multido[region;i;j;k]

         gradvelface(chf_ix[i;j;k]) =
     $        ( velcomp(chf_ix[i   ;j   ;k   ])
     $        - velcomp(chf_ix[i-ii;j-jj;k-kk]) )/dx

         chf_enddo

      else

         chf_multido[centerbox;i;j;k]

         gradvelface(chf_ix[i;j;k]) =
     $        ( gradvelcell(chf_ix[i   ;j   ;k   ])
     $        + gradvelcell(chf_ix[i-ii;j-jj;k-kk]) )/two

         chf_enddo

         if(haslo .eq. 1) then

            chf_multido[lobox;i;j;k]

            gradvelface(chf_ix[i;j;k]) =
     $           (three*gradvelcell(chf_ix[i   ;j   ;k   ])
     $           -      gradvelcell(chf_ix[i+ii;j+jj;k+kk]))/two

            chf_enddo

         endif

         if(hashi .eq. 1) then

            chf_multido[hibox;i;j;k]

            gradvelface(chf_ix[i;j;k]) =
     $           (three*gradvelcell(chf_ix[i-  ii;j-  jj;k-  kk])
     $           -      gradvelcell(chf_ix[i-2*ii;j-2*jj;k-2*kk]))/two

            chf_enddo

         endif
      endif

      return
      end

      subroutine celldivincrvtop(
     $     chf_fra1[divvel],
     $     chf_const_fra[vel],
     $     chf_const_real[dx],
     $     chf_const_int[divdir],
     $     chf_box[region])

      integer chf_ddecl[ii,i;jj,j;kk,k]

      chf_dterm[
      ii = chf_id(divdir, 0);
      jj = chf_id(divdir, 1);
      kk = chf_id(divdir, 2)]

      chf_multido[region;i;j;k]

      divvel(chf_ix[i;j;k]) = divvel(chf_ix[i;j;k]) +
     $     (    vel(chf_ix[i+ii;j+jj;k+kk],divdir)
     $     -    vel(chf_ix[i-ii;j-jj;k-kk],divdir) )/(two*dx)

      chf_enddo

      return
      end

      subroutine facedivincrvtop(
     $     chf_fra1[divvel],
     $     chf_const_fra[vel],
     $     chf_const_fra[gradvel],
     $     chf_box[region],
     $     chf_box[centerbox],
     $     chf_box[lobox],
     $     chf_const_int[haslo],
     $     chf_box[hibox],
     $     chf_const_int[hashi],
     $     chf_const_real[dx],
     $     chf_const_int[facedir],
     $     chf_const_int[divdir],
     $     chf_const_int[gradcomp])

      integer chf_ddecl[ii,i;jj,j;kk,k]

      chf_dterm[
      ii = chf_id(facedir, 0);
      jj = chf_id(facedir, 1);
      kk = chf_id(facedir, 2)]

      if (facedir .eq. divdir) then

         chf_multido[region;i;j;k]

         divvel(chf_ix[i;j;k]) = divvel(chf_ix[i;j;k]) +
     $        (    vel(chf_ix[i   ;j   ;k   ],facedir)
     $        -    vel(chf_ix[i-ii;j-jj;k-kk],facedir) )/dx

         chf_enddo

      else
c     remember this is partial vdivdir/partial xdivdir
         chf_multido[centerbox;i;j;k]

         divvel(chf_ix[i;j;k]) = divvel(chf_ix[i;j;k]) +
     $        ( gradvel(chf_ix[i   ;j   ;k   ], gradcomp)
     $        + gradvel(chf_ix[i-ii;j-jj;k-kk], gradcomp) )/two

         chf_enddo

         if(haslo .eq. 1) then
            chf_multido[lobox;i;j;k]

            divvel(chf_ix[i;j;k]) = divvel(chf_ix[i;j;k]) +
     $           (three*gradvel(chf_ix[i   ;j   ;k   ], gradcomp)
     $           -      gradvel(chf_ix[i+ii;j+jj;k+kk], gradcomp))/two

            chf_enddo
         endif

         if(hashi .eq. 1) then
            chf_multido[hibox;i;j;k]

            divvel(chf_ix[i;j;k]) = divvel(chf_ix[i;j;k]) +
     $           (three*gradvel(chf_ix[i-  ii;j-  jj;k-  kk], gradcomp)
     $           -      gradvel(chf_ix[i-2*ii;j-2*jj;k-2*kk], gradcomp))/two

            chf_enddo
         endif
      endif

      return
      end

      subroutine decrinvrelcoefvtop(
     &     chf_fra[relcoef],
     &     chf_fra[eta],
     &     chf_fra[lambda],
     &     chf_const_real[beta],
     &     chf_box[box],
     $     chf_real[dx],
     $     chf_int[idir],
     $     chf_int[ncomp])

      integer chf_ddecl[ii;jj;kk]
      integer chf_ddecl[i;j;k]
      integer icomp
      real_t lamh, laml,etah, etal, relcoold, relconew, incr
      chf_dterm[
      ii = chf_id(idir, 0);
      jj = chf_id(idir, 1);
      kk = chf_id(idir, 2)]

      do icomp = 0, ncomp-1
         chf_multido[box;i;j;k]

         relcoold = relcoef(chf_ix[i;j;k],icomp)
         etah =    eta(chf_ix[i+ii;j+jj;k+kk],0)
         etal =    eta(chf_ix[i   ;j   ;k   ],0)

         lamh = lambda(chf_ix[i+ii;j+jj;k+kk],0)
         laml = lambda(chf_ix[i   ;j   ;k   ],0)

         if(icomp .eq. idir) then

            incr =
     $         beta*(
     $        two*eta(chf_ix[i+ii;j+jj;k+kk],0) +
     $        lambda (chf_ix[i+ii;j+jj;k+kk],0) +
     $        two*eta(chf_ix[i   ;j   ;k   ],0) +
     $        lambda (chf_ix[i   ;j   ;k   ],0)
     $        )/(dx*dx)

         else
            incr =
     $         beta*(
     $        eta(chf_ix[i+ii;j+jj;k+kk],0) +
     $        eta(chf_ix[i   ;j   ;k   ],0)
     $        )/(dx*dx)

         endif

         relcoef(chf_ix[i;j;k], icomp) = relcoef(chf_ix[i;j;k],icomp) - incr
         relconew = relcoef(chf_ix[i;j;k],icomp)

         chf_enddo
      enddo

      return
      end

C     initialize lambda = alpha*acoef
      subroutine initializerelaxcoef(
     &     chf_fra[relcoef],
     &     chf_fra1[acoef],
     $     chf_real[alpha],
     $     chf_box[box],
     $     chf_int[ncomp])

      integer chf_ddecl[i;j;k]
      integer icomp

      do icomp = 0, ncomp-1
         chf_multido[box;i;j;k]

         relcoef(chf_ix[i;j;k], icomp) = alpha*acoef(chf_ix[i;j;k])

         chf_enddo
      enddo

      return
      end

      subroutine invertlambdavtop(
     &     chf_fra[lambda],
     $     chf_real[safety],
     &     chf_box[box],
     $     chf_int[ncomp])

      integer chf_ddecl[i;j;k]
      integer icomp,jcomp
c      real_t maxval
      real_t zeroval

      zeroval = 1.0e-20

         chf_multido[box;i;j;k]
c         maxval = lambda(chf_ix[i;j;k],0)
c         do jcomp = 1, ncomp-1
c            if(lambda(chf_ix[i;j;k],jcomp) .gt. maxval) then
c               maxval = lambda(chf_ix[i;j;k],jcomp)
c            endif
c         enddo

         do icomp = 0, ncomp-1
            lambda(chf_ix[i;j;k], icomp) = safety/lambda(chf_ix[i;j;k], icomp)
c remove hardwired zero value since it can cause problems in cases where
c the coefficients are very large (DFM -- 6/13/2001)
c            if (abs(lambda(chf_ix[i;j;k], icomp)).le. zeroval) then
c               call MAYDAY_ERROR();
c            endif
         enddo

         chf_enddo

      return
      end

      subroutine gsrbvtop(
     &     chf_fra[phi],
     &     chf_const_fra[lphi],
     &     chf_const_fra[rhs],
     &     chf_const_fra[lambda],
     &     chf_box[coloredbox],
     $     chf_int[ncomp])

      integer chf_ddecl[i;j;k]
      integer icomp
      REAL_T phio, lamo, rhso, lphio

      do icomp = 0, ncomp-1
         chf_multido[coloredbox;i;j;k;2]

         phio = phi(   chf_ix[i;j;k],icomp)
         lamo = lambda(chf_ix[i;j;k],icomp)
         rhso = rhs(   chf_ix[i;j;k],icomp)
         lphio = lphi(  chf_ix[i;j;k],icomp)

         phi(chf_ix[i;j;k], icomp) =
     $        phi(   chf_ix[i;j;k],icomp) +
     &        lambda(chf_ix[i;j;k],icomp)*(
     $        rhs(   chf_ix[i;j;k],icomp) -
     $        lphi(  chf_ix[i;j;k],icomp))

         chf_enddo
      enddo

      return
      end

      subroutine adddivfluxdirvtop(
     &     chf_fra[lhs],
     &     chf_const_fra[flux],
     &     chf_box[region],
     &     chf_const_real[dx],
     $     chf_const_int[ncomp],
     $     chf_const_int[facedir])

      integer chf_ddecl[ii,i;jj,j;kk,k]
      integer icomp

      chf_dterm[
      ii = chf_id(facedir, 0);
      jj = chf_id(facedir, 1);
      kk = chf_id(facedir, 2)]

c     flux already has beta

      do icomp = 0, ncomp-1
         chf_multido[region; i; j; k]

         lhs(chf_ix[i;j;k], icomp) = lhs(chf_ix[i;j;k], icomp)
     $        +
     $        (flux(chf_ix[i+ii;j+jj;k+kk], icomp)
     $        -flux(chf_ix[i   ;j   ;k   ], icomp))/dx

         chf_enddo
      enddo

      return
      end

      subroutine linearextrapvtop(CHF_FRA[phi],
     &                            CHF_BOX[ghostBox],
     &                            CHF_INT[dir],
     &                            CHF_INT[hiLo])

      integer n
      integer CHF_AUTODECL[i]
      integer CHF_AUTODECL[ii]

      CHF_AUTOID[ii; dir; hiLo]

      do n=0, CHF_NCOMP[phi]-1
         CHF_AUTOMULTIDO[ghostBox; i]
           phi(CHF_AUTOIX[i],n) = two*phi(CHF_OFFSETIX[i;-ii],n)
     &                              - phi(CHF_OFFSETIX[i;-2*ii],n)
         CHF_ENDDO
      enddo

      return
      end

      subroutine SLOPESVTOP(CHF_FRA[slopes],
     &                      CHF_CONST_FRA[coarse],
     &                      CHF_BOX[cBox],
     &                      CHF_INT[dir])

      integer n, CHF_AUTODECL[i], CHF_AUTODECL[ii]

      CHF_AUTOID[ii; dir; 1]

      do n=0, CHF_NCOMP[coarse]-1
         CHF_AUTOMULTIDO[cBox;i]
         slopes(CHF_AUTOIX[i],n) = 0.5*(coarse(CHF_OFFSETIX[i;+ii],n)
     &                                 -coarse(CHF_OFFSETIX[i;-ii],n))
         CHF_ENDDO
      enddo

      return
      end
